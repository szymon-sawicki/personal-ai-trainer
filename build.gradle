plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "6.25.0"
}

group = 'net.szymonsawicki'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'dev.langchain4j:langchain4j-ollama-spring-boot-starter:1.0.0-alpha1'
    implementation 'dev.langchain4j:langchain4j-spring-boot-starter:1.0.0-alpha1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    // MapStruct dependencies
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'org.zalando:logbook-spring-boot-starter:3.5.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Lombok + MapStruct binding
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Add this to ensure correct annotation processor order
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.defaultComponentModel=spring'
    ]
}

spotless {
    java {
        // Use the default importOrder configuration
        importOrder()
        toggleOffOn()
        removeUnusedImports()
        googleJavaFormat()
    }
}